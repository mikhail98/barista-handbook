plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'kotlin-parcelize'
}

apply from: "dependencies.gradle"
apply from: "version.gradle"

Properties properties = new Properties()
properties.load(project.rootProject.file('ci.properties').newDataInputStream())

android {
    compileSdkVersion sdk.compileSdkVersion
    buildToolsVersion sdk.buildToolsVersion

    defaultConfig {
        applicationId sdk.applicationId
        minSdkVersion sdk.minSdkVersion
        targetSdkVersion sdk.targetSdkVersion
        versionCode appVersion.versionCode
        versionName appVersion.versionName

        archivesBaseName = archives.archivesBaseName
        resConfigs "en", "ru"

        buildConfigField("String", "ARCHIEVE_NAME", "\"$archives.archivesBaseName\"")
    }

    signingConfigs {
        release {
            storeFile file(properties.getProperty("keyFile"))
            storePassword properties.getProperty("storePassword")
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyPassword")
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            shrinkResources true

            debuggable false
            jniDebuggable false
            zipAlignEnabled true
            crunchPngs false

            proguardFiles 'proguard-rules.pro'
        }
        debug {
            proguardFiles 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility sdk.javaVersion
        targetCompatibility sdk.javaVersion
    }

    kotlinOptions {
        jvmTarget = sdk.jvmTargetVersion
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation library.appCompat
    implementation library.materialComponents
    implementation library.koin
    implementation library.viewModel
    implementation library.gson
    implementation library.encryptedPrefs
    implementation library.csvReader
    implementation library.glide

    implementation platform(library.firebaseBom)
    implementation library.firebaseAnalyticsKtx
    implementation library.firebaseCrashlyticsKtx

    implementation library.dotsIndicator

    annotationProcessor library.glideAnnotationProcessor
}